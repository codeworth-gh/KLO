[>initialFilter-fired< ask:
  {text: Did you lose your job while realising your health-related benefits?}
  {answers:
    {yes: [>rej-endOfWork< reject: Please use our End of Employment interview, at: https://klo-rights.codeworth.io/models/end-of-employment/latest]}
  }
]
[set: Duties += supportingMedicalDocumentation]
[>initialFilter-missWork< ask:
  {text: Did you miss work beacuse of a medical condition?}
  {terms:
    {miss work: Days or hours}
    {medical condition: Ephemeral or permanent health issues, either yours or of someone related to you. }
  }
  {answers:
    {no: [>rej-initialFilter< reject: You may be eligible for benefits, but not from the legislation covered by this interview.]}
  }
]
[>who< ask:
  {text: Who suffers from the health condition in question?}
  {terms:
    {child: Including when the child is over 18.}
  }
  {answers:
    {me:
      [set: RelationToSickPerson=self]
      [call: pSelfCondition]
    }
    {my spouse:
      [set: RelationToSickPerson=spouseOfSick]
      [todo: Handle spouse illness issues]
    }
    {my parent:
      [set: RelationToSickPerson=childOfSick]
      [todo: Handle parent health issues]
    }
    {my child:
      [set: RelationToSickPerson=parentOfSick]
      [call: pChildHealthIssues]
    }
  }
]

<**
 Handling with medical condition of the interwiewee.
*>
[-->pSelfCondition<
  [call: pCaseProperties]

  [consider: <-- TODO move to conditional answers when implemented.
    {slot: Gender}
    {options:
      {male:
        [>cause-male< ask:
          {text: What caused the health condision in question?}
          {terms:
            {health condition: Could be, for example,  illness or injury.}
          }
          {answers:
            {accident: [call: pAccidents]}
            {temporary illness (medium/light): [call: pSickDaysCalcTemp][todo: temporary illness (medium/light)]}
            {serious illness or disability: [todo: serious illness or disability]}
            {work-related illness: [todo: work-related illness]}
          }
        ]
      }
      {female:
        [>cause-female< ask:
          {text: What caused the health condision in question?}
          {terms:
            {health condition: Could be, for example,  illness or injury.}
          }
          {answers:
            {accident: [call: pAccidents]}
            {temporary illness (medium/light): [call: pSickDaysCalcTemp][todo: temporary illness (medium/light)]}
            {serious illness or disability: [todo: serious illness or disability]}
            {work-related illness: [todo: work-related illness]}
            {pregnancy:
              [>pregrnancy-complications< ask:
                {text: Were you instructed by a doctor not to work during the pregrnancy?}
                {answers:
                  {yes: [set: Pregnancy=complicated]}
                  {no:  [set: Pregnancy=regular]}
                }
              ]
            }
            {miscarrige: [todo: miscarrige]}
            {fertility treatments: [todo: fertility treatments]}
          }
        ]
      }
    }
  ]
--]

[-->pAccidents<
  [>accident-type< ask:
    {text: Describe the accident}
    {terms:
      {work related: An accident that happend during work, or on the way to/from it.}
      {non work-related: An accident that did not happen at work, or on the way to/from work. }
    }
    {answers:
      {road accident:
        [>accident-road-filter< ask:
          {text: Was the travel related to your work?}
          {terms:
            {work related: Travel to/from work, during work, or because of work (e.g. getting a visa).}
          }
          {answers:
            {yes: [call: pWorkRelatedAcciedentBenefits] }
            {no:
              [set: SimpleBenefits+=roadAccidentPayment, freeMedicalCare]
            }
          }
        ]
      }
      {work related: [call: pWorkRelatedAcciedentBenefits]}
      {non-work related:
        [set: SicknessPay = gradual]
        [consider:
          {slot: LegalStatus}
          {options:
            { israeliCitizenship: [set: Recommendations+=niiAccidentPayment] }
          }
        ]
      }
    }
  ]
--]

[-->pWorkRelatedAcciedentBenefits<
[todo: Implement work related acciedent benefits.]
--]


<**
Initial case data gatehring
*>
[-->pCaseProperties<
[>status< ask:
  {text: What is your current status in Israel? }
  {answers:
    {citizen: [set: LegalStatus=israeliCitizenship]}
    {visa of type b1: [set: LegalStatus=b1Visa]}
    {no permit:       [set: LegalStatus=noPermit]}
    {palestinian:     [set: LegalStatus=palestinian] }
  }
]
[>gender< ask:
  {text: Are you a woman?}
  {answers:
    {yes: [set: Gender=female]}
    {no: [set: Gender=male]}
  }
]
[>sEmploymentForm< section:
  {title: Form of employment}
  [>employmentUnits< ask:
    {text: How is your salary calculated?}
    {answers:
      {monthly: [set: SalaryUnits=monthly] }
      {daily:
        [set: SalaryUnits=daily]
        [call: pCheckIllnessOnRelevantDay]
      }
      {hourly:
         [set: SalaryUnits=hourly]
         [call: pCheckIllnessOnRelevantDay]
      }
    }
  ]
  [>employemntType< ask:
    {text: How are you employed?}
    {terms: }
    {answers:
      {self employed/freelance: [set: EmploymentType=selfEmployed]}
      {direct: [set: EmploymentType=employee]}
    }
  ]
  [>employmentScope< ask:
    {text: What is the scope of your employment?}
    {answers:
      {partial: [set: EmploymentScope=partial] }
      {full:    [set: EmploymentScope=full] }
      {varied:  [set: EmploymentScope=varied] }
    }
  ]
  [>employmentDuration< ask:
    {text: Are you employed more than one year at your current employer?}
    {answers:
      {yes: [set: EmploymentFlags+=employed1YearAndAbove]}
    }
  ]
  [>employmentSector< ask:
    {text: Select the sector you are employed in:}
    {answers:
      {civil service: [set: Sector=civilService]}
      {manpower: [set: Sector=manpower]}
      {house keeping: [set: Sector=houseKeeping]}
      {security: [set: Sector=security]}
      {event facilities: [set: Sector=eventFacilities]}
      {hotels: [set: Sector=hotels]}
      {none of the above: [set: Sector=other]}
    }
  ]
] <-- / employment form section
--]


[-->pChildHealthIssues<
  [call: pCaseProperties]
  [>child-otherAdults< ask:
    {text: Are there any other adults that have legal custody over the sick child?}
    {terms:
      {other adults: Including spouses. In foster families, this also includes the biological parents of the child.}
    }
    {answers:
      {yes: [set: Duties+=declaration_OnlyAdultToUseBenefits] }
    }
  ]
  [>child-sicknessTypes< ask:
    {text: Does the child suffers from any of the following: }
    {answers:
      {malignant condition:
        [>child-over18-m< ask:
          {text: Is the child at least 18 years old?}
          {answers:
            {no:
              [set: ChildSickness+=dialysisOrMaligantChildPay]
            }
          }
        ]
      }
      {disability:
        [>child-disabilityScope< ask:
          {text: Is the disability your child suffers from permanent?}
          {answers:
            {no: [set: ChildSickness+=sickChildDays][end] }
            {yes: [set: ChildSickness+=disabledChildPay] }
          }
        ]
        [>child-over18-dis< ask:
          {text: Is the child at least 18 years old?}
          {answers:
            {yes:
              [set: Duties+=guardianDocument]
            }
          }
        ]
      }
      {needs dialysis:
        [>child-over18-dia< ask:
          {text: Is the child at least 18 years old?}
          {answers:
            {no:
              [set: ChildSickness+=dialysisOrMaligantChildPay]
            }
          }
        ]
      }
      {none of the above:
        [>child-over16< ask:
          {text: Is the child at least 16 years old?}
          {answers:
            {no: [set:ChildSickness+=sickChildPay]}
          }
        ]
      }
    }
  ]
--]

[-->pCheckIllnessOnRelevantDay<
  [>partialWorkWhen< ask:
    {text: Did the illness occur in one of your workign days?}
    {answers:
        {no: [>partialWorkReject-time< reject: You are not eligible to benefits from the employer, since the illness occured on a day you were not supposed to work anyway. ]}
    }
  ]
--]

[-->pSickDaysCalcTemp<
  [set: SicknessPay = gradual]
  [consider:
    {slot: Sector}
    {options:
      {houseKeeping: [set: SicknessPay = fullFromDayOne]}
      {civilService: [set: SicknessPay = fullFromDayOne]}
    }
  ]
--]
