[call: pCaseProperties]

[>who< ask:
  {text: Who suffers from the health condition in question?}
  {answers:
    {me: [call: pSelfCondition]}
    {my spouse: }
  }
]

<**
 This part deals with medical condition of the interwiewee.
*>
[-->pSelfCondition<
  [call: pCaseProperties]
  [>cause< ask:
    {text: What caused the health condision in question?}
    {terms:
      {health condition: Could be, for example,  illness or injury.}
    }
    {answers:
      {accident: [call: pAccidents]}
      {temporary illness (medium/light): [todo: temporary illness (medium/light)]}
      {serious illness or disability: [todo: serious illness or disability]}
      {work-related illness: [todo: work-related illness]}
      {pregnancy:
        [>pregrnancy-complications< ask:
          {text: Were you instructed by a doctor not to work during the pregrnancy?}
          {answers:
            {yes: [set: Pregnancy=complicated]}
            {no:  [set: Pregnancy=regular]}
          }
        ]
      }
      {miscarrige: [todo: miscarrige]}
      {fertility treatments: [todo: fertility treatments]}
    }
  ]
--]

[-->pAccidents<
  [>accident-type< ask:
    {text: Describe the accident}
    {terms:
      {work related: An accident that happend during work, or on the way to/from it.}
      {non work-related: An accident that did not happen at work, or on the way to/from work. }
    }
    {answers:
      {road accident:
        [>accident-road-filter< ask:
          {text: Was the travel related to your work?}
          {terms:
            {work related: Travel to/from work, during work, or because of work (e.g. getting a visa).}
          }
          {answers:
            {yes: [call: pWorkRelatedAcciedentBenefits] }
            {no:
              [set: SimpleBenefits+=roadAccidentPayment, freeMedicalCare]
            }
          }
        ]
      }
      {work related: [call: pWorkRelatedAcciedentBenefits]}
      {non-work related:
        [set: SimpleBenefits += sicknessPay]
        [consider:
          {slot: LegalStatus}
          {options:
            { israeliCitizenship: [set: Recommendations+=niiAccidentPayment] }
          }
        ]
      }
    }
  ]
--]

[-->pWorkRelatedAcciedentBenefits<
[todo: Implement work related acciedent benefits.]
--]


<**
Initial case data gatehring
*>
[-->pCaseProperties<
[>status< ask:
  {text: What is your current status in Israel? }
  {answers:
    {citizen: [set: LegalStatus=israeliCitizenship]}
    {visa of type b1: [set: LegalStatus=b1Visa]}
    {other: [set: LegalStatus=other] }
  }
]
[>gender< ask:
  {text: Are you a woman?}
  {answers:
    {yes: [set: Gender=female]}
    {no: [set: Gender=male]}
  }
]
[>sEmploymentForm< section:
  {title: Form of employment}
  [>employmentUnits< ask:
    {text: How is your salary calculated?}
    {answers:
      {monthly: [set: SalaryUnits=monthly] }
      {daily:   [set: SalaryUnits=daily] }
      {hourly:  [set: SalaryUnits=hourly] }
    }
  ]
  [>employmentType< ask:
    {text: How are you employed? }
    {answers:
      { direct:          [set: EmploymentType=direct] }
      { freelance:       [set: EmploymentType=freelance] }
      { contractor:      [set: EmploymentType=contractor] }
      { jointEmployment: [set: EmploymentType=jointEmployment] }
    }
  ]
  [>employmentScope< ask:
    {text: What is the scope of your employment?}
    {answers:
      {partial: [set: EmploymentScope=partial] }
      {full:    [set: EmploymentScope=full] }
      {varied:  [set: EmploymentScope=varied] }
    }
  ]
  [>employmentSector< ask:
    {text: Select the sector you are employed in:}
    {answers:
      {civil service: [set: Sector=civilService]}
      {manpower: [set: Sector=manpower]}
      {house keeping: [set: Sector=houseKeeping]}
      {security: [set: Sector=security]}
      {event facilities: [set: Sector=eventFacilities]}
      {hotels: [set: Sector=hotels]}
      {none of the above: [set: Sector=other]}
    }
  ]
] <-- / employment form
--]
