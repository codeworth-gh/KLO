<-- Detecting person properties
[set: EmployerObligations += finalAccountSettlement,workPeriodLetter,form161 ]
[>sInitialFiltering< section:
  {title: Initial Questionnaire}
  [>gender< ask:
    {text: Are you a woman?}
    {answers:
      {yes: [set: Flags+=female]}
    }
  ]
  [>AgeGroup< ask:
    {text: What's your age group?}
    {terms:
      {retirement age: Varies, add a link or a table here.}
    }
    {answers:
      {pre 21: [set: AgeGroup=under21]}
      {before retirement: [set: AgeGroup=workForce]}
      {after retirement:  [set: AgeGroup=pension]}
    }
  ]
  [>status< ask:
    {text: What's your current status in Israel? }
    {answers:
      {citizen: [set: LegalStatus=israeliCitizenship]}
      { palestinian with work permit: [set: LegalStatus=palestinianWorkPermit]}
      { visa of type b1Construction: [set: LegalStatus=b1Construction]}
      { visa of type 2a5: [set: LegalStatus=_2a5]}
      { visa of type b1CareGiver: [set: LegalStatus=b1CareGiver]}
      { visa of type B1General or A5: [set: LegalStatus=residencyProcess]}
      { visa of type b1Agriculture: [set: LegalStatus=b1Agriculture]}
      { visa of type a5: [set: LegalStatus=a5]}
      { visa of type b2: [set: LegalStatus=b2]}
      { undocumented: [set: LegalStatus=undocumented] }
    }
  ]
]

[>sReasonForLeaving< section:
  {title: Reason for leaving}
  [>rflHowEnd< ask:
    {text: How did the employent end?}
    {answers:
      {my initiative:
        [set: Duties += employeePriorNotice]
        [>ReasonForLeaving-self< ask:
          {text: What is the reason for your decision to leave this job?}
          {answers:
            {health issues:  [call: part-health]}
            {enrolled to civil service: }
            {parenting: [call:part-parenting]}
            {disease: }
            {moved: [call:part-moved]} <-- TODO need to show only to israeliCitizenship, residencyProcess
            {worker death: }
            {family member disease: }
            {significant compensation reduction: }
            {significant breaching of rights: }
            {resignation (other): }
          }
        ]
      }
      {involuntary:
        [>ReasonForLeaving-forced< ask:
          {text: Why did you have to leave this job?}
          {answers:
            {end of constract: }
            {visa termination: }
            {employer death: }
            {employer hospitalized: }
            {employer moved to nursing home: }
            {employer choice: [call: gradeProcess] }
          }
        ]
      }
    }
  ]
]

<*
[>occupationSector< ask:
  {text: What sector do you work in?}
  {answers:
    {manpower: [set: Sector=manpower]}
    {}
  }
]
*>
[>employmentUnits< ask:
  {text: How was your salary claculated?}
  {answers:
    {monthly: [set: Employment/SalaryUnits=monthly] }
    {daily:   [set: Employment/SalaryUnits=daily] }
    {hourly:  [set: Employment/SalaryUnits=hourly] }
  }
]
[>employmentType< ask:
  {text: How were you employed? }
  {answers:
    { direct:          [set: Employment/Type=direct] }
    { freelance:       [set: Employment/Type=freelance]
                       [set: Recommendations+=consultLawyer] }
    { contractor:      [set: Employment/Type=contractor] }
    { jointEmployment: [set: Employment/Type=jointEmployment] }
  }
]
[>employmentScope< ask:
  {text: What was the scope of your employment?}
  {answers:
    {partial: [set: Employment/Scope=partial] }
    {full:    [set: Employment/Scope=full] }
    {varied:  [set: Employment/Scope=varied] }
  }
]

<--

[-->part-health<
  {title: Health issues}
  [>hc-acc-cause< ask:
    {text: The health issue that made me leave is:}
    {answers:
      {accident during my work:}
      {accident on my way to work:}
      {road accident: }
      {health condition of a family member:
        [set: ReasonForLeaving = familyMemberDisease]
        [>hc-acc-fm< ask:
          {text: Is the sick family member? }
          {answers:
            {my child, parent, or spouse:[call: leaveForMedicalConditions]}
            {my grandchild, grandfather, or parent in law:
              [>hc-acc-fm-1< ask:
                {text: Are you living with your sick relative, and providing for him?}
                {terms:
                  {providing: Taking active part in financing his/her needs. }
                }
                {answers:
                  {yes: [call: leaveForMedicalConditions]}
                  {no: [call: benefitSet-resignation]}
                }
              ]
            }
            {other family member: [call: benefitSet-resignation] }
          }
        ]
      }
      {my health condition:
        [set: ReasonForLeaving=medicalCondition]
        [>hc-acc-adj< ask:
          {text: can your workplace be adjusted to your medical condition?}
          {answers:
            {yes, employer refused:
              [call: benefitSet-severance]
              [set: Benefits/Properties+=specialCompensations]
            }
            {yes, I don't want to stay: [call: benefitSet-resignation]}
            {no:
              [call: leaveForMedicalConditions]
              [set: Benefits/Properties+=disabilityAllowance; Pension=allowance]
            }
          }
        ]
        [call: leaveForMedicalConditions]
      }
    }
  ]
--]

[-->leaveForMedicalConditions<
  <* This part works on leaving a workplace for medical conditions,
   * when the client may be eligible for benefits.
  *>
  {title: Leaving a workplace for medical conditions}
  [>lfmm-1< ask:
    {text: Did you inform your employer the reason for leaving, and give them a chance to adjust your employment conditions?}
    {answers:
      {yes: [set: Benefits/Properties+=severancePay; UnemploymentBenefits=immediate]}
      {no:  [call: benefitSet-resignation]}
    }
  ]
--]

[-->gradeProcess<
[>gp-hearing< ask:
  {text: Did you get a hearing prior to being fired?}
  {terms:
    {hearing: A meeting with the employer to declared as "before job termination" talk.}
  }
  {answers:
    {no:
      [set: ProcessFairness=flawed]
    }
  }
]
[>gp-hearing-details< ask:
  {text: Was one of the following reasons insinuated as the reason for your job termination? (list goes here)}
  {terms: }
  {answers:
    {yes:
      [set: ProcessFairness=illegal; Recommendations+=sueFormerEmployerSoon]
    }
    {no:
      [>gp-complaint< ask:
        {text: Were you fired after filing a complaint or getting advice from a lawyer?}
        {answers:
          {yes:
            [set: ProcessFairness=illegal; Recommendations+=sueFormerEmployerSoon]
          }
        }
      ]
    }
  }
]
[set: ProcessFairness=ok] <-- ignored if the value is already set.
--]

[-->part-parenting<
  [>prnt-time< ask:
    {text: Did more than 9 month pass since the day you've recevied the child}
    {terms:
      {day: Date of birth, adoption, or other way of becoming a legal guardian of the child.}
    }
    {answers:
      {no:
        [>prnt-f-forChild< ask:
          {text: Did you resign for the sake of the child?}
          {terms:
            {Sake of child: E.g. to be more with them, reduce commute times etc.}
          }
          {answers:
            {yes:
              [when:
                { Flags+=female:
                    [when: <-- todo: really TimeConstraints>over6Months:
                      {TimeConstraints=over6Months: }
                      {else: [set: Benefits/Properties+=severancePay] }
                    ][end] }
                {else:
                  [>prnt-m-forChild< ask:
                    {text: Are you single, or does your wife has a job? }
                    {answers:
                      {yes:
                        [when: <-- todo: really TimeConstraints>over6Months:
                          {TimeConstraints=over6Months: }
                          {else: [set: Benefits/Properties+=severancePay] }
                        ][end]
                  }}]
                }
              ]
            }
          }
        ]
      }
    }
  ]
  [call: benefitSet-resignation]
--]


[-->part-moved<
[>moved-who< ask:
  {text: Who moved?}
  {answers:
    {me:
      [>moved-me-filter< ask:
        {text: Is you new place in Israel? }
        {answers:
          {no:[call: benefitSet-resignation]}
          {yes:
            [set: Duties+=provideEvidenceForNewLocation]
            [>moved-dispatch-reason< ask:
              {text: Did you move because of status change regarding your spouse?}
              {terms:
                {status change: Marriage, divorce, moving in togetehr...}
              }
              {answers:
                {yes:
                  [>moved-spouse-40km< ask:
                    {text: Is your new home at least 40 km from your old home?}
                    {answers:
                      {no: [call: benefitSet-resignation]}
                      {yes:
                        [>moved-following-spouse< ask:
                          {text: Which of the following apply}
                          {terms:
                            {Moved in with spouse: The spouse has to live there before, not just you two guys moving in together.}
                          }
                          {answers:
                            {Moved in with spouse: [call: benefitSet-severance] }
                            {My spouse moved because of work: [call: benefitSet-severance] }
                            {Divorce: [call: benefitSet-severance]}
                            {Other: [call: benefitSet-resignation]}
                          }
                        ]
                      }
                    }
                  ]
                }
                {no:
                  [>moved-no-spouse< ask:
                    {text: Did you move ot any of the following}
                    {answers:
                      {to an agricaltural settlement:
                         [set: Recommendations+=eligibleForRetroactiveTaxBenefitsWithinAYear]
                         [call: benefitSet-severance] }
                      {to an eligible settelment:
                        [set: Recommendations+=eligibleForRetroactiveTaxBenefitsWithinAYear]
                        [call: benefitSet-severance] }
                      {to a settlement outside the green line:
                        [set: Recommendations+=eligibleForRetroactiveTaxBenefitsWithinAYear]
                        [call: benefitSet-severance] }
                      {other: [call: benefitSet-resignation]}
                    }
                  ]
                }
              }
            ]
          }
        }
      ]
    }
    {my employer:
      [>moved-employer< ask:
        {text: Will working in the new location will significantly affect your quality of life?}
        {terms:
          {quality of life: This depends a lot on the specific case. }
        }
        {answers:
          {yes:
            [set: Duties += provideDetailedResoningQoL]
            [call: benefitSet-severance]
          }
          {no: [call:benefitSet-resignation]}
        }
      ]
    }
  }
]
--]

<*********************
  Benefit sets
*********************>
[-->benefitSet-severance<
  {title: Benefits for Severance}
  [set:  UnemploymentBenefits=immediate]
  [consider:
    {slot: TimeConstraints}
    {options: <-- TODO: improve once boolean conditions are implemented
      {over11Months:  [set: Benefits/Properties+=severancePay]}
      {over51Months:  [set: Benefits/Properties+=severancePay]}
      {over63Months: [set: Benefits/Properties+=severancePay]}
      {over8Years:   [set: Benefits/Properties+=severancePay]}
      {over10Years:  [set: Benefits/Properties+=severancePay]}
      {over13Years:  [set: Benefits/Properties+=severancePay]}
    }
  ]
--]

[-->benefitSet-resignation<
  {title: Benefits for Resignation}
  [consider:
    {slot: LegalStatus}
    {options:
      {israeliCitizenship:
        [set: UnemploymentBenefits=after90Days]
  }}]
--]
