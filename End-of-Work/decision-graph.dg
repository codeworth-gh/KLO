<-- Detecting person properties
[set: EmployerObligations += finalAccountSettlement,workPeriodLetter,form161 ]
[>sInitialFiltering< section:
  {title: Initial Questionnaire}
  [>gender< ask:
    {text: Are you a woman?}
    {answers:
      {yes: [set: Flags+=female]}
    }
  ]
  [>AgeGroup< ask:
    {text: What's your age group?}
    {terms:
      {retirement age: Varies, add a link or a table here.}
    }
    {answers:
      {pre 21: [set: AgeGroup=under21]}
      {before retirement: [set: AgeGroup=workForce]}
      {after retirement: [set: AgeGroup=pension]}
    }
  ]
  [>status< ask:
    {text: What's your current status in Israel? }
    {answers:
      {citizen: [set: LegalStatus=israeliCitizenship]}
      { palestinian with work permit: [set: LegalStatus=palestinianWorkPermit]}
      { visa of type b1Construction: [set: LegalStatus=b1Construction]}
      { visa of type _2a5: [set: LegalStatus=_2a5]}
      { visa of type b1CareGiver: [set: LegalStatus=b1CareGiver]}
      { visa of type b1General: [set: LegalStatus=b1General]}
      { visa of type b1Agriculture: [set: LegalStatus=b1Agriculture]}
      { visa of type a5: [set: LegalStatus=a5]}
      { visa of type b2: [set: LegalStatus=b2]}
      { undocumented: [set: LegalStatus=undocumented] }
    }
  ]
]

[>sReasonForLeaving< section:
  {title: Reason for leaving}
  [>rflHowEnd< ask:
    {text: How did the employent end?}
    {answers:
      {my initiative:
        [set: Duties += employeePriorNotice]
        [consider:
          {slot: LegalStatus}
          {options:
            {israeliCitizenship:
              [>rflMyInitiative< ask:
                {text: Why did you leave?}
                {answers:
                  {health issues: [call: health-citizen]}
                  {end of constract: }
                  {worker death: }
                  {employer death: }
                  <-- {enrolled to civil service:} <-- to enable only for AgeGroup=under21
                  {significant compensation reduction: }
                  {significant breaching of rights: }
                  {parenting: }
                  {family member disease: }
                  {moved: }
                  {resignation for other reasons: }
                }
              ]
            }
          }
          {else:
          }
        ]
      }
      {involuntary: }
    }
  ]
]
[>whyLeave< ask:
  {text: What is the reason for leaving this job?}
  {answers:
    {resignation: }
    {visa termination: }
    {disease: }
    {end of constract: }
    {worker death: }
    {employer death: }
    {employer hospitalized: }
    {employer moved to nursing home: }
    {enrolled to civil service: }
    {significant compensation reduction: }
    {significant breaching of rights: }
    {parenting: }
    {family member disease: }
    {moved: } <-- distinguish between marriage, eligible locations, other reasons.
  }
]

<*
[>occupationSector< ask:
  {text: What sector do you work in?}
  {answers:
    {manpower: [set: Sector=manpower]}
    {}
  }
]
*>
[>employmentUnits< ask:
  {text: How was your salary claculated?}
  {answers:
    {monthly: [set: Employment/SalaryUnits=monthly] }
    {daily:   [set: Employment/SalaryUnits=daily] }
    {hourly:  [set: Employment/SalaryUnits=hourly] }
  }
]
[>employmentType< ask:
  {text: How were you employed? }
  {answers:
      { direct:          [set: Employment/Type=direct] }
      { freelance:       [set: Employment/Type=freelance] }
      { contractor:      [set: Employment/Type=contractor] }
      { jointEmployment: [set: Employment/Type=jointEmployment] }
  }
]
[>employmentScope< ask:
  {text: What was the scope of your employment?}
  {answers:
    {partial: [set: Employment/Scope=partial] }
    {full:    [set: Employment/Scope=full] }
    {varied:  [set: Employment/Scope=varied] }
  }
]

<--

[-->health-citizen<
  {title: Health issues}
  [>hc-acc-cause< ask:
    {text: The health issue that made me leave is:}
    {answers:
      {accident during my work:}
      {accident on my way to work:}
      {road accident: }
      {health condition of a family member:
        [set: ReasonForLeaving = familyMemberDisease]
        [>hc-acc-fm< ask:
          {text: Is the sick family member? }
          {answers:
            {my child, parent, or spouse:[call: leaveForMedicalConditions]}
            {my grandchild, grandfather, or parent in law:
              [>hc-acc-fm-1< ask:
                {text: Are you living with your sick relative, and providing for him?}
                {terms:
                  {providing: Taking active part in financing his/her needs. }
                }
                {answers:
                  {yes: [call: leaveForMedicalConditions]}
                  {no: [call: benefitSet-resignation]}
                }
              ]
            }
            {other family member: [call: benefitSet-resignation] }
          }
        ]
      }
      {my health condition:
        [set: ReasonForLeaving=medicalCondition]
        [>hc-acc-adj< ask:
          {text: can your workplace be adjusted to your medical condition?}
          {answers:
            {yes, employer refused:
              [call: benefitSet-severance]
              [set: Benefits/Properties+=specialCompensations]
            }
            {yes, I don't want to stay: [call: benefitSet-resignation]}
            {no:
              [call: leaveForMedicalConditions]
              [set: Benefits/Properties+=disabilityAllowance; Pension=allowance]
            }
          }
        ]
        [call: leaveForMedicalConditions]
      }
    }
  ]
--]

[-->leaveForMedicalConditions<
  <* This part works on leaving a workplace for medical conditions,
   * when the client may be eligible for benefits.
  *>
  {title: Leaving a workplace for medical conditions}
  [>lfmm-1< ask:
    {text: Did you inform your employer the reason for leaving, and give them a chance to adjust your employment conditions?}
    {answers:
      {yes: [set: Benefits/Properties+=severancePay; UnemploymentBenefits=immediate]}
      {no:  [call: benefitSet-resignation]}
    }
  ]
--]

[-->benefitSet-resignation<
  {title: Benefits for Resignation}
  [consider:
    {slot: LegalStatus}
    {options:
      {israeliCitizenship:
        [set: UnemploymentBenefits=after90Days]
  }}]
--]

<** זכויות עבור פיטורים *>
[-->benefitSet-severance<
  {title: Benefits for Severance}
  [set:  UnemploymentBenefits=immediate]
  [consider:
    {slot: TimeConstraints}
    {options: <-- TODO: improve once boolean conditions are implemented
      {over11Month:  [set: Benefits/Properties+=severancePay]}
      {over51Month:  [set: Benefits/Properties+=severancePay]}
      {over63Months: [set: Benefits/Properties+=severancePay]}
      {over8Years:   [set: Benefits/Properties+=severancePay]}
      {over10Years:  [set: Benefits/Properties+=severancePay]}
      {over13Years:  [set: Benefits/Properties+=severancePay]}
    }
  ]
--]
