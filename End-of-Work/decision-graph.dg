[#import trailings: trailings.dg]
<-- Detecting person properties
[set: EmployerObligations += finalAccountSettlement,workPeriodLetter,form161,jobTerminationConfirmation ]
[>sInitialFiltering< section:
  {title: Initial Questionnaire}
  [>gender< ask:
    {text: Are you a woman?}
    {answers:
      {yes: [set: Gender=female]}
      {no: [set: Gender=male]}
    }
  ]
  [>sAgeGroup< section:
    {title: Age group}
    [consider:
      {slot: Gender}
      {options:
        {male:
          [>AgeGroupMale< ask:
            {text: How old are you? }
            {answers:
              {under 67:    [set: AgeGroup=workForce]}
              {67 and over:  [set: AgeGroup=pension; Pension=allowance]}
            }
          ]
        }
        {female:
          [>AgeGroupFemale< ask:
            {text: How old are you? }
            {answers:
              {under 62:    [set: AgeGroup=workForce]}
              {62 to 67:    [set: AgeGroup=voluntaryPension; Pension=allowance; Recommendations+=checkElderyAllowance]}
              {67 and over: [set: AgeGroup=pension; Pension=allowance; Recommendations+=checkElderyAllowance]}
            }
          ]
        }
      }
    ]
  ]
  [>status1< ask:
    {text: Are you an Israeli citizen?}
    {answers:
      {yes: [set: LegalStatus=israeliCitizenship; EmployerObligations+=pensionFundNotice] }
      {no:
        [>status-foreign< ask:
          {text: What's your current status in Israel? }
          {answers:
            {palestinian with work permit: [set: LegalStatus=palestinianWorkPermit]}
            {visa of type b1Construction: [set: LegalStatus=b1Construction; Flags+=nonIsraeliWorker; Sector=construction]}
            {visa of type 2a5: [set: LegalStatus=_2a5; Flags+=nonIsraeliWorker]}
            {visa of type b1CareGiver: [set: LegalStatus=b1CareGiver; Sector=careGiving; Flags+=nonIsraeliWorker]}
            {visa of type b1Agriculture: [set: LegalStatus=b1Agriculture; Sector=agriculture; Flags+=nonIsraeliWorker]}
            {undocumented: [set: LegalStatus=undocumented; Flags+=nonIsraeliWorker] }
            {other non-citizen: [set: LegalStatus=other]}
          }
        ]
      }
    }
  ]
  [>duration-employment< ask:
    {text: Were you employed for more than 11 months?}
    {answers:
      {yes: [set: Flags+=thisEmploymentOver11months] }
    }
  ]
  [consider:
    {slot: LegalStatus}
    {options:
      {b1CareGiver:
        [>duration-inIsrael< ask:
          {text: How long have you been in Israel?}
          {answers:
            {Less than 51 Months: [set: TimeInIsrael=lessThan51Months] }
            {51 Months or more: [set: TimeInIsrael=overOr51Months] }
            {Over 63 Months: [set: TimeInIsrael=over63Months] }
            {Over 8 Years:   [set: TimeInIsrael=over8Years]   }
            {Over 10 Years:  [set: TimeInIsrael=over10Years]  }
            {Over 13 Years:  [set: TimeInIsrael=over13Years]  }
          }
        ]
      }
    }
  ]
]

[>sReasonForLeaving< section:
  {title: Reason for leaving}
  [>rflHowEnd< ask:
    {text: How did the employent end?}
    {answers:
      {my initiative:
        [set: Duties += employeePriorNotice]
        [>ReasonForLeaving-self< ask:
          {text: What is the reason for your decision to leave this job?}
          {answers:
            {health issues:  [call: part-health]}
            {enrolled to civil service: [set: ReasonForLeaving=enrolledToCivilService][call: benefitSet-severance] }
            {parenting: [set:ReasonForLeaving=forParenting][call:part-parenting] }
            {moved: [call:part-moved]} <-- TODO need to show only to israeliCitizenship
            {worker death:
              [set: Recommendations+=payKeens; ReasonForLeaving=workerDeath]
              [call: benefitSet-severance]
            }
            {significant deterioration of employment conditions:
              [set: Duties+=employeePriorNoticeForEmploymentChange; ReasonForLeaving=significantCompensationReduction]
              [call: benefitSet-severance]
            }
            {significant breaching of rights:
              [set: Duties+=employeePriorNoticeForEmploymentChange; ReasonForLeaving=significantBreachingOfRights]
              [call: benefitSet-severance]
            }
            {severe abuse:
              [set: Recommendations+=involveAidOrganizations; ReasonForLeaving=severeAbuse]
              [call: benefitSet-severance]
            }
            {retirement:
              [set: ReasonForLeaving=retirement]
              [call: benefitSet-severance]
            }
            {I was sexually harrased:
              [set: ReasonForLeaving = sexualHarrasment;
                    Properties       += specialCompensations;
                    Recommendations  += canReportToThePolice]
              [call: benefitSet-severance]
            }
            {resignation (other): [call: benefitSet-resignation]}
          }
        ]
      }
      {involuntary:
        [set: EmployerObligations+=priorNotice, hearing]<-- TODO: do these apply to care givers?
        [call: benefitSet-severance]
        [consider: {slot: LegalStatus}
          {options:
            {israeliCitizenship:
              [>ReasonForLeaving-forced-citizens< ask:
                {text: Why did you have to leave this job?}
                {terms:
                  {employer collapse: Chapter 11, bankrupcy }
                  {employer change: Company takover, nursing a wife and then moving to nursing her husband.}
                }
                {answers:
                  {end of contract:   [set: ReasonForLeaving=endOfContract]}
                  {employer death:    [set: ReasonForLeaving=employerDeath]}
                  {employer choice:   [call: pEmployerChoice]}
                  {employer collapse: [call: pEmployerCollapsed]}
                  {employer changed:  [set: ReasonForLeaving=employerChanged]}
                }
              ]
            }
            {b1CareGiver:
              [>ReasonForLeaving-forced-b1< ask:
                {text: Why did you have to leave this job?}
                {terms:
                  {employer collapse: Chapter 11, bankrupcy }
                  {employer change: Company takover, nursing a wife and then moving to nursing her husband.}
                }
                {answers:
                  {end of contract:   [set: ReasonForLeaving=endOfContract]}
                  {employer death:    [set: ReasonForLeaving=employerDeath]}
                  {visa termination:  [set: ReasonForLeaving=visaTermination]}
                  {employer terminally hospitalized: [set: ReasonForLeaving=employerHospitalized]}
                  {employer moved to nursing home: [set: ReasonForLeaving=employerMovedToNursingHome]}
                  {employer choice:   [call: pEmployerChoice]}
                  {employer collapse: [call: pEmployerCollapsed]}
                  {employer changed:  [set: ReasonForLeaving=employerChanged]}
                }
              ]
            }
          }
          {else:
            [>ReasonForLeaving-forced-others< ask:
              {text: Why did you have to leave this job?}
              {terms:
                {employer collapse: Chapter 11, bankrupcy }
                {employer change: Company takover, nursing a wife and then moving to nursing her husband.}
              }
              {answers:
                {end of contract:   [set: ReasonForLeaving=endOfContract]}
                {employer death:    [set: ReasonForLeaving=employerDeath]}
                {visa termination:  [set: ReasonForLeaving=visaTermination]}
                {employer choice:   [call: pEmployerChoice]}
                {employer collapse: [call: pEmployerCollapsed]}
                {employer changed:  [set: ReasonForLeaving=employerChanged]}
              }
            ]
          }
        ]
      }
    }
  ]
]

[>sEmploymentForm< section:
  {title: Form of employment}
  [>employmentUnits< ask:
    {text: How was your salary calculated?}
    {answers:
      {monthly: [set: Employment/SalaryUnits=monthly] }
      {daily:   [set: Employment/SalaryUnits=daily] }
      {hourly:  [set: Employment/SalaryUnits=hourly] }
    }
  ]
  [>employmentType< ask:
    {text: How were you employed? }
    {answers:
      { direct:          [set: Employment/Type=direct] }
      { freelance:       [set: Employment/Type=freelance; Recommendations+=consultLawyer] }
      { contractor:      [set: Employment/Type=contractor] }
      { jointEmployment: [set: Employment/Type=jointEmployment] }
    }
  ]
  [>employmentScope< ask:
    {text: What was the scope of your employment?}
    {answers:
      {partial: [set: Employment/Scope=partial] }
      {full:    [set: Employment/Scope=full] }
      {varied:  [set: Employment/Scope=varied] }
    }
  ]
]

[consider:
  {slot: LegalStatus}
  {options:
    {israeliCitizenship:}
    {palestinianWorkPermit:}
  }
  {else:
    [>planToLeaveOrStay< ask:
      {text: Do you plan to stay in Israel?}
      {answers:
          {yes:
            [set: Recommendations+=reducedBenefitsForOverstay2Month]
            [consider:
              {slot: Sector}
              {options:
                {careGiving: [call: trailings>careGivers] }
                {construction: [call: trailings>constructionWorkers]}
              }
            ]
          }
          {no: [call: pPlanningToLeaveIsrael]}
      }
    ]
  }
]

<*******************
 END OF CORE
*******************>

[-->part-health<
  {title: Health issues}
  [>hc-acc-cause< ask:
    {text: The health issue that made me leave is:}
    {answers:
      {accident during my work:}
      {accident on my way to work:}
      {road accident: }
      {health condition of a family member:
        [set: ReasonForLeaving = familyMemberDisease]
        [>hc-acc-fm< ask:
          {text: Is the sick family member? }
          {answers:
            {my child, parent, or spouse:[call: leaveForMedicalConditions]}
            {my grandchild, grandfather, or parent in law:
              [>hc-acc-fm-1< ask:
                {text: Are you living with your sick relative, and providing for him?}
                {terms:
                  {providing: Taking active part in financing his/her needs. }
                }
                {answers:
                  {yes: [call: leaveForMedicalConditions]}
                  {no: [call: benefitSet-resignation]}
                }
              ]
            }
            {other family member: [call: benefitSet-resignation] }
          }
        ]
      }
      {my health condition:
        [set: ReasonForLeaving=medicalCondition]
        [>hc-acc-adj< ask:
          {text: can your workplace be adjusted to your medical condition?}
          {answers:
            {yes, employer refused:
              [call: benefitSet-severance]
              [set: Benefits/Properties+=specialCompensations]
            }
            {yes, I don't want to stay: [call: benefitSet-resignation]}
            {no:
              [call: leaveForMedicalConditions]
              [set: Benefits/Properties+=disabilityAllowance; Pension=allowance]
            }
          }
        ]
        [call: leaveForMedicalConditions]
      }
    }
  ]
--]

[-->leaveForMedicalConditions<
  <* This part works on leaving a workplace for medical conditions,
   * when the worker may be eligible for benefits.
  *>
  {title: Leaving a workplace for medical conditions}
  [>lfmm-1< ask:
    {text: Did you inform your employer the reason for leaving, and give them a chance to adjust your employment conditions?}
    {answers:
      {yes: [set: Benefits/Properties+=severancePay; UnemploymentBenefits=immediate]}
      {no:  [call: benefitSet-resignation]}
    }
  ]
--]

[-->pEmployerChoice<
[when:
  { Gender=female:
    [>isPregnant< ask:
      {text: Were you pregnant when your employment ended?}
      {answers:
        {yes: [set: Flags += pregnant, possiblyIllegalSeverance;
                    Properties+=severanceIllegalUnlessApprovedByLaborMinstry]}
      }
    ]
  }
]
[when:
  {Flags+=pregnant: }
  {else:
    [>maternityLeave< ask: <-- TODO There's a comment about asking only women, but men can take maternaty leave too, right?
      {text: Were you fired during maternity leave?}
      {terms:
        {maternity leave: including for fathers and adopting parents.}
      }
      {answers:
        {yes: [set: Flags += possiblyIllegalSeverance]}
      }
    ]
    [>insinuatedReasons< ask:
      {text: Was one of the following reasons insinuated as the reason for your job termination? (list goes here)}
      {answers:
        {yes:
          [set: ProcessFairness=illegal; Recommendations+=sueFormerEmployerSoon]
        }
        {no:
          [>gp-complaint< ask:
            {text: Were you fired after filing a complaint or getting advice from a lawyer?}
            {answers:
              {yes:
                [set: Flags += possiblyIllegalSeverance; ProcessFairness=illegal;
                      Recommendations+=sueFormerEmployerSoon; Properties+=severanceCancellation]
              }
            }
          ]
        }
      }
    ]
  }
]
[consider:
  {slot: LegalStatus}
  {options:
    {israeliCitizenship:
      [>reserveDuty< ask:
        {text: Where you fired during reserve duty, or within 30 days of completing one?}
        {answers:
          {yes: [set: Flags+=possiblyIllegalSeverance;
            Properties+=severanceIllegalUnlessApprovedByDefenseMinstry]}
        }
      ]
    }
  }
]
[>gp-hearing< ask:
  {text: Did you get a hearing prior to being fired?}
  {terms:
    {hearing: A meeting with the employer to declared as "before job termination" talk.}
  }
  {answers:
    {no:  [set: ProcessFairness=flawed]}
    {yes: [set: ProcessFairness=ok]}
  }
]
--]

[-->part-parenting<
  [>prnt-time< ask:
    {text: Did more than 9 month pass since the day you've recevied the child}
    {terms:
      {day: Date of birth, adoption, or other way of becoming a legal guardian of the child.}
    }
    {answers:
      {no:
        [>prnt-f-forChild< ask:
          {text: Did you resign for the sake of the child?}
          {terms:
            {Sake of child: E.g. to be more with them, reduce commute times etc.}
          }
          {answers:
            {yes:
              [when:
                { Gender=female:
                    [set: Benefits/Properties+=severancePay][end]}
                {else:
                  [>prnt-m-forChild< ask:
                    {text: Are you single, or does your wife has a job? }
                    {answers:
                      {yes: [set: Benefits/Properties+=severancePay][end]}}
                  ]
  }]}}]}}]
  [call: benefitSet-resignation]
--]


[-->part-moved<
[>moved-who< ask:
  {text: Who moved?}
  {answers:
    {me:
      [>moved-me-filter< ask:
        {text: Is you new place in Israel? }
        {answers:
          {no:[call: benefitSet-resignation]}
          {yes:
            [set: Duties+=provideEvidenceForNewLocation, employeePriorNoticeForEmploymentChange]
            [>moved-dispatch-reason< ask:
              {text: Did you move because of status change regarding your spouse?}
              {terms:
                {status change: Marriage, divorce, moving in together...}
              }
              {answers:
                {yes:
                  [set: ReasonForLeaving=marriageAndMoving]
                  [>moved-spouse-40km< ask:
                    {text: Is your new home at least 40 km from your old home?}
                    {answers:
                      {no: [call: benefitSet-resignation]}
                      {yes:
                        [>moved-following-spouse< ask:
                          {text: Which of the following apply}
                          {terms:
                            {Moved in with spouse: The spouse has to live there before, not just you two guys moving in together.}
                          }
                          {answers:
                            {Moved in with spouse: [call: benefitSet-severance] }
                            {My spouse moved because of work: [call: benefitSet-severance] }
                            {Divorce: [call: benefitSet-severance]}
                            {Other: [call: benefitSet-resignation]}
                          }
                        ]
                      }
                    }
                  ]
                }
                {no:
                  [>moved-no-spouse< ask:
                    {text: Did you move to any of the following}
                    {answers:
                      {to an agricaltural settlement:
                         [set: Recommendations+=eligibleForRetroactiveTaxBenefitsWithinAYear]
                         [call: benefitSet-severance] }
                      {to an eligible settelment:
                        [set: Recommendations+=eligibleForRetroactiveTaxBenefitsWithinAYear]
                        [call: benefitSet-severance] }
                      {to a settlement outside the green line:
                        [set: Recommendations+=eligibleForRetroactiveTaxBenefitsWithinAYear]
                        [call: benefitSet-severance] }
                      {other: [call: benefitSet-resignation]}
                    }
                  ]
                }
              }
            ]
          }
        }
      ]
    }
    {my employer:
      [>moved-employer< ask:
        {text: Will working in the new location will significantly affect your quality of life?}
        {terms:
          {quality of life: This depends a lot on the specific case. }
        }
        {answers:
          {yes:
            [set: Duties += provideDetailedResoningQoL]
            [call: benefitSet-severance]
          }
          {no: [call:benefitSet-resignation]}
        }
      ]
    }
  }
]
--]

<*********************
  Benefit sets
*********************>
[-->benefitSet-severance<
  {title: Benefits for Severance}
  [set: EffectiveTerminationType=severance]
  [consider:
    {slot: LegalStatus}
    {options:
      {israeliCitizenship:
        [consider:
          {slot: AgeGroup }
          {options:
            { workForce: [set: UnemploymentBenefits=immediate] }
        }]
      }
    }
    {else:
      [set: Properties+=severancePay]
    }
  ]

  [when:
    { Flags+=thisEmploymentOver11months: [set: Benefits/Properties+=severancePay] }
  ]
  [todo: benefitSet-severance make more accurate]
--]

[-->benefitSet-resignation<
  {title: Benefits for Resignation}
  [set: EffectiveTerminationType=resignation]
  [consider:
    {slot: LegalStatus}
    {options:
      {israeliCitizenship:
        [set: UnemploymentBenefits=after90Days]
  }}]
  [todo: benefitSet-resignation make more accurate]
--]

[-->pPlanningToLeaveIsrael<
  [>cg-whyLeave< ask:
    {text: What is the reason for your intension to leave Israel?}
    {answers:
      {visa termination: [call: benefitSet-severance]}
      {my medical condition:
        [>cg-medicalConditionWorkRelated< ask:
          {text: Do you think your medical condition is related to your work?}
          {answers:
            {yes: [set: Recommendations+=applyForWorkAccident; Properties+=eligibleForFlightExpensePI]}
            {no: [set: Properties+=eligibleForFlightExpenseNI]}
          }
        ]
        [consider:
          {slot: TimeInIsrael}
          {options:
            {over10Years: [set: Properties += possible80KGrant]}
            {over13Years: [set: Properties += possible80KGrant]}
          }
        ]
      }
      {caring for a family member:
        [call: benefitSet-severance]
        [set: Duties+=provideEvidenceForCaringForFamily ]
      }
      {other: [call:benefitSet-resignation]}
    }
  ]
--]

[-->pEmployerCollapsed<
[set: ReasonForLeaving=employerCollapse]
[>employerCollapseFormalBankrupcy< ask:
  {text: Is the employer formally declared as bankrupt?}
  {answers:
    {yes: [set: Properties+=bankruptcyAllowance]}
    {no: [set: Recommendations+=uniteEmployeesToDeclareBankrupcy]}
  }
]
--]
